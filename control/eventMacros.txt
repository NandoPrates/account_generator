automacro moveToSprakkiOutside {
	exclusive 1
	InMap new_1-1, new_2-1, new_3-1, new_4-1, new_5-1
	BaseLevel = 1
	NpcNotNear /Gramps/
	timeout 10
	call {
		do move 143 111
	}
}

automacro TalkSprakkiOutside {
	exclusive 1
	InMap new_1-1, new_2-1, new_3-1, new_4-1, new_5-1
	BaseLevel = 1
	run-once 1
	NpcNear /Gramps/
	call {
		[
			do conf autoMoveOnDeath 1
			do conf autoMoveOnDeath_x none
			do conf autoMoveOnDeath_y none
			do conf autoMoveOnDeath_map none
			do conf storageAuto_npc none
			do conf saveMap none
			do conf lockMap none
			do conf saveMap_warpToBuyOrSell 0
			do conf sellAuto 0
			do conf storageAuto 0
			do conf storageAuto 0
			do conf sellAuto 0
			do conf route_randomWalk 1
			do conf itemsTakeAuto 2
			do conf itemsGatherAuto 0
			do conf autoTalkCont 1
			do conf attackAuto 2
			do conf attackAuto_party 1
			do conf aggressiveAntiKS 1
			do conf attackUseWeapon 1
			do conf skillsAddAuto 1
			do conf skillsAddAuto_list NV_BASIC 9, Heal 5, Increase AGI 10, Divine Protection 5, Blessing 10, Ruwach 1, Teleport 2, Heal 10, Warp Portal 4, Angelus 7, PR_MAGNIFICAT 5, PR_IMPOSITIO 5, PR_MACEMASTERY 10
			do conf statsAddAuto 1
			do conf statsAddAuto_dontUseBonus 1
			do conf statsAddAuto_list 20 str,30 agi,20 int,20 dex,50 agi,40 str,50 dex, 30 int,80 agi
			do conf -f QuestPart none
			do conf -f Grinding none
			do conf -f current_npc none
			do conf -f saveMap_save_sequence none
			do conf -f saveMap_storage_sequence none
			do conf -f saveMap_wanted none
			do conf -f saveMap_kafra_position none
			do conf -f saveMap_sellNpc_position none
			do conf -f In_saveMap_sequence false
		]
		do talk $.NpcNearLastBinId
		do talk resp /No/
		do talk resp /No/
		do talk resp /No/
		do talk resp /No/
		do talk resp /No/
		do talk resp /Yes/
		do talk $.NpcNearLastBinId
		
	}
}

automacro talk_toSpraki{
	exclusive 1
	InMap new_1-2, new_2-2, new_3-2, new_4-2, new_5-2
	BaseLevel = 1
	NpcNotNear /Sprakki/
	call {
		do move 101 29
	}
}

automacro moveInsideBugged1 {
	exclusive 1
	QuestInactive 7117
	BaseLevel = 2
	InMap new_1-1, new_2-1, new_3-1, new_4-1, new_5-1
	priority 1
	call GotoInside
}

automacro moveInsideBugged2 {
	exclusive 1
	QuestActive 7118
	BaseLevel = 2
	InMap new_1-1, new_2-1, new_3-1, new_4-1, new_5-1
	priority 1
	call GotoInside
}

macro GotoInside {
	$nextMap = nextMap("$.map")
	do move $nextMap 101 29
}

automacro moveNextToSprakki {
	exclusive 1
	QuestActive 7117
	NpcNotNear /Sprakki/
	BaseLevel = 1
	InMap new_1-2, new_2-2, new_3-2, new_4-2, new_5-2
	priority 1
	call GotoSpraki
}

automacro moveNextToSprakkiBugged1 {
	exclusive 1
	QuestInactive 7117
	BaseLevel = 2
	NpcNotNear /Sprakki/
	InMap new_1-2, new_2-2, new_3-2, new_4-2, new_5-2
	priority 1
	call GotoSpraki
}

automacro moveNextToSprakkiBugged2 {
	exclusive 1
	QuestActive 7118
	BaseLevel = 2
	NpcNotNear /Sprakki/
	InMap new_1-2, new_2-2, new_3-2, new_4-2, new_5-2
	priority 1
	call GotoSpraki
}

macro GotoSpraki {
	do move 101 29
}

automacro talkSprakkiBugged1 {
	exclusive 1
	QuestInactive 7117
	BaseLevel = 2
	NpcNear /Sprakki/
	InMap new_1-2, new_2-2, new_3-2, new_4-2, new_5-2
	priority 0
	call SprakiiBuggedTalk
}

automacro talkSprakkiBugged2 {
	exclusive 1
	QuestActive 7118
	BaseLevel = 2
	NpcNear /Sprakki/
	InMap new_1-2, new_2-2, new_3-2, new_4-2, new_5-2
	priority 0
	call SprakiiBuggedTalk
}

macro SprakiiBuggedTalk {
	do talk $.NpcNearLastBinId
	
}

automacro talkSprakki {
	exclusive 1
	QuestActive 7117
	BaseLevel = 1
	NpcNear /Sprakki/
	InMap new_1-2, new_2-2, new_3-2, new_4-2, new_5-2
	priority 0
	call {
		do talk $.NpcNearLastBinId
		do talk resp 0
	}
}

automacro moveNextToBrade1 {
	exclusive 1
	QuestActive 7118
	NpcNotNear /Brade/
	InMap new_1-2, new_2-2, new_3-2, new_4-2, new_5-2
	priority 2
	call MoveToBrade
}

automacro moveNextToBrade2 {
	exclusive 1
	QuestActive 7119
	NpcNotNear /Brade/
	InMap new_1-2, new_2-2, new_3-2, new_4-2, new_5-2
	priority 2
	call MoveToBrade
}

macro MoveToBrade {
	do move 103 105
}

automacro talkBrade {
	exclusive 1
	QuestActive 7118
	BaseLevel = 2
	NpcNear /Brade/
	InMap new_1-2, new_2-2, new_3-2, new_4-2, new_5-2
	priority 0
	call {
		do talk $.NpcNearLastBinId
	}
}


automacro moveNextToBradeBugged {
	exclusive 1
	QuestInactive 7118
	QuestInactive 7119
	BaseLevel = 3
	InInventoryID 5055 = 0
	NpcNotNear /Brade/
	InMap new_1-2, new_2-2, new_3-2, new_4-2, new_5-2
	priority 1
	call MoveToBrade
}

automacro talkBradeBugged {
	exclusive 1
	QuestInactive 7118
	QuestInactive 7119
	BaseLevel = 3
	InInventoryID 5055 = 0
	NpcNear /Brade/
	InMap new_1-2, new_2-2, new_3-2, new_4-2, new_5-2
	priority 0
	call {
		do talk $.NpcNearLastBinId
	}
}

automacro equipStuffForBrade {
	exclusive 1
	QuestActive 7119
	IsNotEquippedID topHead 5055, leftHand 2112, robe 2510, shoes 2414, armor 2352, rightHand 1243
	call {
		$check = GetIndexAndEquipped("topHead", 5055)
		if ($check != -1) do eq $check
		$check = GetIndexAndEquipped("robe", 2510)
		if ($check != -1) do eq $check
		$check = GetIndexAndEquipped("shoes",2414)
		if ($check != -1) do eq $check
		$check = GetIndexAndEquipped("armor",2352)
		if ($check != -1) do eq $check
		$check = GetIndexAndEquipped("leftHand",2112)
		if ($check != -1) do eq $check
		$check = GetIndexAndEquipped("rightHand",1243)
		if ($check != -1) do eq $check
	}
}

automacro talkBradeSecond {
	exclusive 1
	QuestActive 7119
	BaseLevel = 3
	IsEquippedID topHead 5055
	IsEquippedID leftHand 2112
	IsEquippedID rightHand 1243
	IsEquippedID robe 2510
	IsEquippedID armor 2352
	IsEquippedID shoes 2414
	NpcNear /Brade/
	call {
		do talk $.NpcNearLastBinId
	}
}

automacro BradeBuggedNo7120MoveJinha {
	exclusive 1
	QuestInactive 7119
	QuestInactive 7120
	BaseLevel = 4
	InInventoryID 12324 > 0
	IsEquippedID topHead 5055
	IsEquippedID leftHand 2112
	IsEquippedID rightHand 1243
	IsEquippedID robe 2510
	IsEquippedID armor 2352
	IsEquippedID shoes 2414
	SkillLevel NV_FIRSTAID = 0
	NpcNotNear /Jinha/
	call MoveJinha
}

automacro moveToGirlSkill {
	exclusive 1
	QuestActive 7120
	NpcNotNear /Jinha/
	SkillLevel NV_FIRSTAID = 0
	priority 0
	call MoveJinha
}

macro MoveJinha {
	do move 107 108
}

automacro BradeBuggedNo7120TalkJinha {
	exclusive 1
	QuestInactive 7119
	QuestInactive 7120
	BaseLevel = 4
	InInventoryID 12324 > 0
	IsEquippedID topHead 5055
	IsEquippedID leftHand 2112
	IsEquippedID rightHand 1243
	IsEquippedID robe 2510
	IsEquippedID armor 2352
	IsEquippedID shoes 2414
	SkillLevel NV_FIRSTAID = 0
	NpcNear /Jinha/
	call TalkJinha
}

automacro talkGirlSkill {
	exclusive 1
	QuestActive 7120
	NpcNear /Jinha/
	SkillLevel NV_FIRSTAID = 0
	priority 0
	call TalkJinha
}

macro TalkJinha {
	do talk $.NpcNearLastBinId
	do talk resp 0
}

automacro BradeBuggedNo7120MoveBrade {
	exclusive 1
	QuestInactive 7119
	QuestInactive 7120
	BaseLevel = 4
	InInventoryID 12324 > 0
	InInventoryID 2393 = 0
	IsEquippedID topHead 5055
	IsEquippedID leftHand 2112
	IsEquippedID rightHand 1243
	IsEquippedID robe 2510
	IsEquippedID armor 2352
	IsEquippedID shoes 2414
	SkillLevel NV_FIRSTAID = 1
	NpcNotNear /Brade/
	call MoveToBrade
}

automacro BradeBuggedNo7120TalkBrade {
	exclusive 1
	QuestInactive 7119
	QuestInactive 7120
	BaseLevel = 4
	InInventoryID 12324 > 0
	InInventoryID 2393 = 0
	IsEquippedID topHead 5055
	IsEquippedID leftHand 2112
	IsEquippedID rightHand 1243
	IsEquippedID robe 2510
	IsEquippedID armor 2352
	IsEquippedID shoes 2414
	SkillLevel NV_FIRSTAID = 1
	NpcNear /Brade/
	call TalkBradeGetTunic
}

automacro talkBradeThird {
	exclusive 1
	QuestActive 7120
	SkillLevel NV_FIRSTAID = 1
	NpcNear /Brade/
	priority 0
	call TalkBradeGetTunic
}

macro TalkBradeGetTunic {
	do talk $.NpcNearLastBinId
}

automacro equipStuffForGirl {
	exclusive 1
	QuestInactive 7120
	InInventoryID 2393 = 1
	InMap new_1-2, new_2-2, new_3-2, new_4-2, new_5-2
	IsNotEquippedID armor 2393
	call {
		$check = GetIndexAndEquipped("armor", 2393)
		if ($check != -1) {
			do eq $check
		}
	}
}

automacro moveNextToChoco {
	exclusive 1
	IsEquippedID armor 2393
	InMap new_1-2, new_2-2, new_3-2, new_4-2, new_5-2
	NpcNotNear /Choco/
	priority 1
	call {
		do move 32 171
	}
}

automacro talkChoco {
	exclusive 1
	QuestInactive 7120
	QuestInactive 7121
	IsEquippedID armor 2393
	InMap new_1-2, new_2-2, new_3-2, new_4-2, new_5-2
	NpcNear /Choco/
	call {
		do talk $.NpcNearLastBinId
		do conf -f  current_npc kafra
	}
}

automacro moveNextToGuys {
	exclusive 1
	QuestActive 7121
	NpcNotNear /(Choco|Kafra|Cool Event|Soldier)/
	priority 0
	call {
		do move 32 171
	}
}

automacro talkToGuysBugged {
	exclusive 1
	QuestActive 7121
	ConfigKeyNotExist current_npc
	NpcNear /Kafra/
	call {
		do conf -f current_npc kafra
	}
}

automacro talkKafra {
	exclusive 1
	QuestActive 7121
	ConfigKey current_npc kafra
	NpcNear /Kafra/
	call {
		do talk $.NpcNearLastBinId
		do talk resp 4
		do conf -f current_npc Cool
	}
}

automacro talkZonda {
	exclusive 1
	QuestActive 7121
	ConfigKey current_npc Cool
	NpcNear /Cool/
	call {
		do talk $.NpcNearLastBinId
		do conf -f current_npc Soldier
	}
}

automacro talkSoldado {
	exclusive 1
	QuestActive 7121
	ConfigKey current_npc Soldier
	NpcNear /Soldier/
	call {
		do talk $.NpcNearLastBinId
		do conf current_npc choco
	}
}

automacro talkChocoAgain {
	exclusive 1
	QuestActive 7121
	ConfigKey current_npc choco
	NpcNear /Choco/
	call {
		do talk $.NpcNearLastBinId
		do conf current_npc kafra
		
	}
}

macro Stop_moving {
	[
	do conf route_randomWalk 0
	do conf attackAuto 1
	]
}
macro Start_moving {
	[
	do conf route_randomWalk 1
	do conf attackAuto 2
	]
}

automacro completeiTodasAsQuests {
	JobLevel = 10
	BaseLevel < 12
	exclusive 1
	run-once 1
	InMap new_1-3, new_2-3, new_3-3, new_4-3, new_5-3
	call {
		[
		do conf route_randomWalk 0
		do conf attackAuto 1
		]
		do move 95 174
	}
}

automacro talk_final_tester {
	JobLevel = 10
	exclusive 1
	run-once 1
	NpcNear /Final Tester/
	call {
		do talk $.NpcNearLastBinId
		do talk resp 0
		do talk resp 1
		do conf -f current_npc Cleric
	}
}

automacro move_aco_changejob {
	JobLevel = 10
	InMap prt_church
	ConfigKey current_npc Cleric
	QuestInactive 1001
	QuestInactive 1002
	QuestInactive 1003
	NpcNotNear /Cleric/
	exclusive 1
	run-once 1
	call {
		do move 183 37
	}
}

automacro talk_aco_changejob {
	JobLevel = 10
	InMap prt_church
	QuestInactive 1001
	QuestInactive 1002
	QuestInactive 1003
	NpcNear /Cleric/
	run-once 1
	call {
		do conf -f QuestPart none
		do talk $.NpcNearLastBinId
		do talk resp 0
	}
}

automacro talk_Rubalkabar {
	QuestActive 1001
	ConfigKeyNot QuestPart finish_quest_acolyte
	InMap prt_church
	run-once 1
	call {
		do move prt_fild03 365 255
		do talknpc 365 255 c
		do move prt_church 184 38
		do conf -f QuestPart finish_quest_acolyte
	}
}

automacro talk_Yosuke {
	QuestActive 1003
	ConfigKeyNot QuestPart finish_quest_acolyte
	InMap prt_church
	run-once 1
	call {
		do move prt_fild00 207 221
		do talknpc 208 218 c
		do move prt_church 184 38
		do conf -f QuestPart finish_quest_acolyte
	}
}

automacro talk_Marthilda {
	QuestActive 1002
	ConfigKeyNot QuestPart finish_quest_acolyte
	InMap prt_church
	run-once 1
	call {
		do move prontera 32 198
		do talknpc 29 207 c r2 c r3
		do move moc_fild07
		do move moc_fild07 39 354
		do talknpc 41 355 c
		do move morocc 164 264
		do talknpc 160 258 c r2 c r0
		do move prt_church 184 38
		do conf -f QuestPart finish_quest_acolyte
	}
}

automacro finish_quest_acolyte{
	QuestActive 1001,1002,1003
	ConfigKey QuestPart finish_quest_acolyte
	InMap prt_church
	NpcNear /Cleric/
	run-once 1
	call {
		do conf -f QuestPart none
		do talk $.NpcNearLastBinId
		do talk resp 0
	}
}

automacro save_Payon_for_lvling {
	JobLevel = 1
	JobID 4
	ConfigKeyNot saveMap payon
	InMap prt_church
	run-once 1
	call {
		call start_moving
		call SetSaveIn "payon"
	}
}

automacro grind_willow {
	BaseLevel 1..21
	InMap payon
	ConfigKey saveMap payon
	ConfigKey In_saveMap_sequence false
	ConfigKeyNot Grinding willow
	run-once 1
	call {
		call lvlVia "willow"
	}

}

automacro grind_zombie {
	BaseLevel 22..34
	ConfigKey In_saveMap_sequence false
	ConfigKeyNot Grinding zombie
	run-once 1
	call lvlVia "zombie"
}

automacro grind_bigfoot {
	BaseLevel 35..50
	ConfigKey In_saveMap_sequence false
	ConfigKeyNot Grinding bigfoot
	run-once 1
	call lvlVia "bigfoot"
}

automacro grind_wolf {
	BaseLevel 98..99
	ConfigKey In_saveMap_sequence false
	ConfigKeyNot Grinding wolf
	run-once 1
	call lvlVia "wolf"
}

automacro save_Morocc_for_lvling {
	BaseLevel 51..60
	ConfigKeyNot saveMap morocc
	run-once 1
	call SetSaveIn "morocc"
}

automacro grind_hode {
	BaseLevel 51..60
	ConfigKey saveMap morocc
	ConfigKey In_saveMap_sequence false
	ConfigKeyNot Grinding hode
	run-once 1
	call lvlVia "hode"
}


macro lvlVia {
	do conf -f Grinding $.param[0]
	switch ( $.param[0] ) {
		case (=~ /willow/i ) {
		   	[
				do conf lockMap pay_fild01
			   	do conf storageAuto 1
			   	do conf sellAuto 1
				do conf buyredpot.disabled 1
				do mconf creamy 0 0 0
			]
		}
		case (=~ /zombie/i ) {
		   	[
			   do conf lockMap pay_dun00
			   do conf storageAuto 1
			   do conf sellAuto 1
			   do conf buyredpot.disabled 0
			]
		}
		case (=~ /bigfoot/i ) {
		   	[
			   do conf lockMap pay_fild07
			   do conf storageAuto 1
			   do conf sellAuto 1
			   do conf buyredpot.disabled 0
			]
		}
		case (=~ /wolf/i ) {
		   	[
			   do conf lockMap pay_fild02
			   do conf storageAuto 1
			   do conf sellAuto 1
			   do conf buyredpot.disabled 0
			]
		}
		case (=~ /hode/i ) {
		   	[
			   do conf lockMap moc_fild17
			   do conf storageAuto 1
			   do conf sellAuto 1
			   do conf buyredpot.disabled 1
			]
		}
	}
}


macro SetSaveIn {
	do conf lockMap none
	do conf -f saveMap_save_sequence c r0
	do conf -f saveMap_storage_sequence c r1
	log $.param[0]
	switch ( $.param[0] ) {
		case (=~ /einbroch/i ) {
			do conf -f saveMap_wanted einbroch
			do conf -f saveMap_kafra_position 242 205
			do conf -f saveMap_sellNpc_position einbroch 224 207
		}
		case (=~ /aldebaran/i ) {
			do conf -f saveMap_wanted aldebaran
			do conf -f saveMap_kafra_position 143 119
			do conf -f saveMap_sellNpc_position aldebaran 165 107
		}
		case (=~ /juno/i ) {
		    do conf -f saveMap_wanted yuno
			do conf -f saveMap_kafra_position 152 187
			do conf -f saveMap_sellNpc_position yuno_in01 25 34
		}
		case (=~ /rachel/i ) {
			do conf -f saveMap_wanted rachel
			do conf -f saveMap_kafra_position 109 138
			do conf -f saveMap_sellNpc_position rachel 65 80
		}
		case (=~ /comodo/i ) {
		    do conf -f saveMap_wanted comodo
			do conf -f saveMap_kafra_position 195 150
			do conf -f saveMap_sellNpc_position cmd_in01 79 182
		}
		case (=~ /prontera/i ) {
		    do conf -f saveMap_wanted prontera
			do conf -f saveMap_kafra_position 151 29
			do conf -f saveMap_sellNpc_position prontera 113 42
		}
		case (=~ /payon/i ) {
			do conf -f saveMap_wanted payon
			do conf -f saveMap_kafra_position 181 104
			do conf -f saveMap_sellNpc_position payon 159 96
		}
		case (=~ /geffen/i ) {
			do conf -f saveMap_wanted geffen
			do conf -f saveMap_kafra_position 120 62
			do conf -f saveMap_sellNpc_position geffen_in 74 144
		}
		case (=~ /morocc|morroc|morooc/i ) {
			do conf -f saveMap_wanted morocc
			do conf -f saveMap_kafra_position 156 97
			do conf -f saveMap_sellNpc_position morocc_in 141 67
		}
		case (=~ /izlude/i ) {
			do conf -f saveMap_wanted izlude
			do conf -f saveMap_kafra_position 134 88
			do conf -f saveMap_sellNpc_position izlude 105 99
		}
		case ( =~ /veins/i ) {
			do conf -f saveMap_wanted veins
			do conf -f saveMap_kafra_position 207 128
			do conf -f saveMap_sellNpc_position ve_in 243 303
		}
		else {
			log nome da cidade inválido, tente novamente...
			log Talvez o nome foi escrito errado?
			stop
		}
	}
	log Iniciando seguência de salvamento em &config(saveMap_wanted)
	$saveMap = &config(saveMap_wanted)
	do conf -f In_saveMap_sequence true
}


automacro EnableTeleBuynStorage {
	exclusive 1
	SkillLevel AL_TELEPORT >= 2
	ConfigKeyNot saveMap_warpToBuyOrSell 1
	call {
		do conf saveMap_warpToBuyOrSell 1
	}
}


automacro movendoPraKafraForaDaCidade {
	exclusive 1
	ConfigKey In_saveMap_sequence true
	ConfigKeyNot saveMap_wanted none
	ConfigKeyNot saveMap $saveMap
	NotInMap $saveMap
	call move_to_near_kafra
}

automacro movendoPraKafraDentroDaCidade {
	exclusive 1
	ConfigKeyNot saveMap $saveMap
	ConfigKeyNot saveMap_wanted none
	NpcNotNear /(Kafra Employee|Funcionária Kafra|Corp. Ev. Incríveis)/
	InMap $saveMap
	call move_to_near_kafra
}

macro move_to_near_Kafra {
	log estou indo para a kafra em $saveMap &config(saveMap_kafra_position)
	do move $saveMap &config(saveMap_kafra_position) &random("1","2","3","4","5")
}

automacro FalarComKafra {
	ConfigKey In_saveMap_sequence true
	ConfigKeyNot saveMap $saveMap
	ConfigKeyNot saveMap_wanted none
	InMap $saveMap
	NpcNear /(Kafra Employee|Funcionária Kafra|Corp. Ev. Incríveis)/
	delay 2
	timeout 20
	call {
		log Falando com kafra na posição &config(saveMap_kafra_position) usando a sequência '&config(saveMap_save_sequence)'
		do talknpc &config(saveMap_kafra_position) &config(saveMap_save_sequence)
	}
}

automacro SalvoNaKafra {
	exclusive 1
	priority -5
	NpcMsgName /Location saved/ /(Kafra Employee|Funcionária Kafra|Corp)/
	ConfigKeyNot saveMap $saveMap
	ConfigKey In_saveMap_sequence true
	InMap $saveMap
	call {
		do conf saveMap $saveMap
		do conf storageAuto_npc $saveMap &config(saveMap_kafra_position)
		do conf storageAuto 1
		do conf sellAuto 1
		do conf sellAuto_npc &config(saveMap_sellNpc_position)
		do conf storageAuto_npc_type 3
		do conf storageAuto_npc_steps &config(saveMap_storage_sequence)
		
		do conf In_saveMap_sequence false
		do conf saveMap_save_sequence none
		do conf saveMap_storage_sequence none
		do conf saveMap_wanted none
		do conf saveMap_kafra_position none
		do conf saveMap_sellNpc_position none
	}
}

automacro portal_block {
	NpcMsg /you want to move/
	timeout 10
	call {
		do talk resp 0
	}
}

sub GetIndexAndEquipped {
	my ($type, $id) = @_;
	my $Item = $char->inventory->getByNameID($id);
	if ($Item eq "" ) {
	message "Erro: ou a ID do item está incorreta, ou você não possui esse equipamento.\n";
	return -1;
	}
	my $EquipIndex = $Item->{binID};
	if (exists $char->{equipment}{$type}) {
		my $equipItem = $char->{equipment}{$type};
		if ($equipItem->{nameID} == $id) {
			message "Erro: Esse equip já está equipado.\n";
			return -1;
		} else {
			return $EquipIndex;
		}
	} else {
		return $EquipIndex;
	}
}


sub GetNamenyNameID {
	my $name = $items_lut{$_[0]};
	return $name;
}

sub nextMap {
	my $map = $_[0];
	if ($map =~ /^new_(\d)-(\d)$/) {
	return "new_".$1."-".($2+1);
	} else {
		return 0;
	}
}